load("//tools/base/bazel:bazel.bzl", "iml_module")
load("//tools/base/bazel:kotlin.bzl", "kotlin_library", "kotlin_test")
load("//tools/base/bazel:proto.bzl", "android_java_proto_library", "java_proto_library")

# managed by go/iml_to_build
iml_module(
    name = "studio.android.sdktools.adblib",
    srcs = ["src"],
    iml_files = ["android.sdktools.adblib.iml"],
    test_srcs = ["test/src"],
    visibility = ["//visibility:public"],
    # do not sort: must match IML order
    deps = [
        "//prebuilts/studio/intellij-sdk:studio-sdk",
        "//tools/adt/idea/.idea/libraries:kotlin-test[test]",
        "//tools/base/fakeadbserver:studio.android.sdktools.fakeadbserver[module, test]",
        "//tools/adt/idea/.idea/libraries:libapp-processes-proto",
    ],
)

# Build adblib as a standalone library, with side effect of ensuring that adblib does not
# use unwanted dependencies from "studio-sdk" in the iml_module rule above
# Build with: bazel build //tools/base/adblib:adblib
kotlin_library(
    name = "adblib",
    srcs = glob([
        "src/**/*.kt",
    ]),
    resource_strip_prefix = "tools/base/adblib",
    visibility = [
        "//tools/base/adb-proxy:__subpackages__",
        "//tools/base/adblib-ddmlibcompatibility:__subpackages__",
        "//tools/base/adblib-tools:__subpackages__",
        "//tools/base/deploy/deployer:__subpackages__",
        "//tools/vendor/google/directaccess-client:__subpackages__",
    ],
    deps = [
        "@maven//:org.jetbrains.kotlin.kotlin-stdlib-jdk8",
        "@maven//:org.jetbrains.kotlinx.kotlinx-coroutines-core",
        # Required to build the java file generated by ":app-processes-proto" rule
        "@maven//:com.google.protobuf.protobuf-java",
        # See rule at end of this file
        ":app-processes-proto",
    ],
)

# Test adblib as a standalone library, with side effect of ensuring that adblib does not
# use unwanted dependencies from "studio-sdk" in the iml_module rule above
# Run tests with: bazel test //tools/base/adblib:adblib.tests.test
kotlin_test(
    name = "adblib.tests",
    srcs = glob([
        "test/src/**/*.kt",
        "test/src/**/*.java",
    ]),
    # So that we can use adblib "internal" classes/functions from Unit Tests
    friends = [":adblib"],
    jvm_flags = ["-Dtest.suite.jar=adblib.tests.jar"],
    test_class = "com.android.testutils.JarTestSuite",
    visibility = [
        "//tools/base/adblib-ddmlibcompatibility:__subpackages__",
        "//tools/base/adblib-tools:__subpackages__",
    ],
    deps = [
        ":adblib",
        # For "JarTestSuite"
        "//tools/base/testutils:tools.testutils",
        # For JUnit4 support
        "@maven//:junit.junit",
        # FakeAdbServer
        "//tools/base/fakeadbserver:tools.fakeadbserver",
    ],
)

#
# Rule to compile 'proto/app_processes.proto' for the stand-alone "kotlin_library" version
# of adblib. This compiles the proto into the "com.android.server.adb.protos" package.
#
java_proto_library(
    name = "app-processes-proto",
    srcs = glob(["proto/app_processes.proto"]),
    visibility = [],
)
